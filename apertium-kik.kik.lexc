! Morphological Transducer for Kikuyu

Multichar_Symbols

! Noun classes
%<cl1%> ! Noun class 1
%<cl3%> ! Noun class 3
%<cl5%> ! Noun class 6
%<cl7%> ! Noun class 7
%<cl9p6%>   ! Noun class 9p6
%<cl9p1%0%>  ! Noun class 9p1%0
%<cl11p6%>  ! Noun class 11p6
%<cl11p1%0%> ! Noun class 11p1%0
%<cl12p6%>  ! Noun class 12p6
%<cl12p13%> ! Noun class 12p13
%<cl14%>    ! Noun class 14
%<cl15%>    ! Noun class 15
%<???%>     ! helpful placeholder

! Part of speech categories
%<n%>   ! Noun
%<v%>   ! Verb
%<adj%> ! Adjective
%<pro%> ! Pronoun
%<prep%> ! preposition?
%<quant%> ! quantifier?

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Tense, Remoteness, and Aspect
%<rempast%>    ! Remote past
%<nearpast%>   ! Near past
%<currpast%>   ! Current past
%<pres%>       ! Present
%<currfut%>    ! Current future
%<remfut%>     ! Remote future

%<hab%>     ! Habitual aspect
%<proc%>    ! Processual aspect
%<perf%>    ! Perfect aspect
%<comlp%>   ! Completive aspect
%<prog%>    ! Progressive aspect

%<seq%>     ! Sequential
%<foc%>     ! Focus

%<refl%>    ! reflexive

! Other verb morphology

%<refl%>  ! Reflexive
%<neg%>   ! Negative
%<trans%> ! Transitive
%<caus%>  ! Causative
%<app%>   ! Applicative
%<recip%> ! Reciprocal
%<revers%>! Reversive 
%<intens%>! Intensive
! Other symbols

%>      ! Morpheme boundary

! Agreement Tags

%[%-sg%]
%[%+sg%]
%[%-pl%]
%[%+pl%]
%[%-foc%]
%[%+foc%]
%[%-seq%]
%[%+seq%]

%[%-rempast%]
%[%-nearpast%]
%[%-currpast%]
%[%-pres%]
%[%-currfut%]
%[%-remfut%]

%[%+rempast%]
%[%+nearpast%]
%[%+currpast%]
%[%+pres%]
%[%+currfut%]
%[%+remfut%]

%[%-p1%]
%[%-p2%]
%[%+p1%]
%[%+p2%]

%[%-cl1%] ! Noun class 1
%[%-cl3%] ! Noun class 3
%[%-cl5%] ! Noun class 6
%[%-cl7%] ! Noun class 7
%[%-cl9p6%]   ! Noun class 9p6
%[%-cl9p1%0%]  ! Noun class 9p1%0
%[%-cl11p6%]  ! Noun class 11p6
%[%-cl11p1%0%] ! Noun class 11p1%0
%[%-cl12p6%] ! Noun class 12p6
%[%-cl12p13%] ! Noun class 12p13
%[%-cl14%]    ! Noun class 14
%[%-cl15%]    ! Noun class 15

%[%+cl1%] ! Noun class 1
%[%+cl3%] ! Noun class 3
%[%+cl5%] ! Noun class 6
%[%+cl7%] ! Noun class 7
%[%+cl9p6%]   ! Noun class 9p6
%[%+cl9p1%0%]  ! Noun class 9p1%0
%[%+cl11p6%]  ! Noun class 11p6
%[%+cl11p1%0%] ! Noun class 11p1%0
%[%+cl12p6%] ! Noun class 12p6
%[%+cl12p13%] ! Noun class 12p13
%[%+cl14%]    ! Noun class 14
%[%+cl15%]    ! Noun class 15

%[%+refl%]
%[%-refl%]
%[%-imp%]
%[%+imp%]
%[%+neg%]
%[%-neg%]

%[%-hab%]
%[%-perf%]
%[%-noasp%]
%[%-compl%]
%[%-proc%]

%[%+hab%]
%[%+perf%]
%[%+noasp%]
%[%+compl%]
%[%+proc%]

%[%-fvA%]
%[%+fvA%]
%[%-fvE%]
%[%+fvE%]
%[%+fvĨ%]
%[%-fvĨ%]

! Archiphonemes

%{A%}
%{K%}
%{C%} 
%{CC%}
%{R%}
%{N%}
%{N2%} 
%{N3%}
%{I%}
%{Ũ%}
%{Ĩ%}
%{T%}
%{U%}
%{E%}
%{O%}
%{RAR%}
%{RRI%}
%{IRI%}
%{Ĩ%}
%{tĨ%}
%{EE%} ! fv e

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounPrefix ;
VerbPrefix ;
AdjPrefix ;
Assoc ;
PersPro ;
LittleWord ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounPluralAgreement
[%+sg%]%<sg%>: # ;
[%+pl%]%<pl%>: # ;

LEXICON NounRootCl1

ndũ:nd%{Ũ%} Cl1 ; ! person
arimũ:arim%{Ũ%} Cl1 ; ! teacher
rĩmi:%{R%}ĩm%{I%} Cl1 ; ! farmer
timia:%{T%}imi%{A%} Cl1 ; ! woman
nyina:nyin%{A%} Cl1 ; ! mother
thomi:thom%{I%} Cl1 ; ! student
thuri:thur%{I%} Cl1 ; ! man
arĩ:%{A%}r%{Ĩ%} Cl1 ; ! daughter

LEXICON NounRootCl1Irr

wagui:wagu%{I%} Cl1 ;

LEXICON Cl1

%<n%>%<cl1%>: NounPluralAgreement;

LEXICON NounRootCl3

aki:%{A%}k%{I%} Cl3 ; ! fire
ithikiri:%{I%}thikir%{I%} Cl3 ; ! bicycle
bira:bir%{A%} Cl3 ; ! ball
rango:rang%{O%} Cl3 ; ! door
ciĩ:%{C%}i%{Ĩ%} Cl3 ; ! home
tĩ:%{T%}%{Ĩ%} Cl3 ; ! tree
cuha:%{C%}uh%{A%} Cl3 ; ! swing
rũthi:%{R%}ũth%{I%} Cl3 ; ! lion

LEXICON Cl3

%<n%>%<cl3%>: NounPluralAgreement ;

LEXICON NounRootCl5

hũa:hũ%{A%} Cl5 ; ! flower
tunda:%{T%}und%{A%} Cl5 ; ! fruit
rima:%{R%}im%{A%} Cl5 ; ! hole
ria:%{R%}i%{A%} Cl5 ; ! milk
bera:ber%{A%} Cl5 ; ! pear
gũrũ:gũr%{Ũ%} Cl5 ; ! leg
aĩ:%{A%}%{Ĩ%} Cl5 ; ! water
higa:hig%{a%} Cl5 ; ! stone
embe:%{E}%mb%{e%} Cl5 ; ! mango
rigũ:%{R%}ig%{Ũ%} Cl5 ; ! banana
inabu:%{I%}nab%{U%} Cl5 ; 
ĩciria:%{Ĩ%}ciri%{A%} Cl5 ;
aĩ:%{A%}%{Ĩ%} Cl5 ; ! water

LEXICON Cl5

%<n%>%<cl5%>: NounPluralAgreement ;

LEXICON NounRootCl7

rĩma:%{R%}ĩm%{A%} Cl7 ; ! mountain
tĩ:%{T%}%{Ĩ%} Cl7 ; ! chair
rio:%{R%}i%{O%} Cl7 ; ! food
kombe:%{K%}omb%{E%} Cl7 ; ! cup
ũra:%{Ũ%}r%{A%} Cl7 ; ! frog
rot:%{R%}ot Cl7 ; ! dream
rathi:%{R%}ath%{A%} Cl7 ; ! class
boko:bok%{O%} Cl7 ;
imba:%{I%}mb%{A%} Cl7 ;

LEXICON Cl7

%<n%>%<cl7%>: NounPluralAgreement ;

LEXICON NounRootCl9p1%0

bata:bat%{A%} Cl9p1%0 ;
buku:buk%{U%} Cl9p1%0 ; ! book
bembe:bemb%{E%} Cl9p1%0 ; ! corn
gathĩ:gath%{Ĩ%} Cl9p1%0 ; ! ladder
gui:gu%{I%} Cl9p1%0 ; ! dog, plural category uncertain
nyamũ:nyam%{Ũ%} Cl9p1%0 ; ! animal, plural category uncertain
cukuru:%{C%}ukur%{U%} Cl9p1%0 ; ! school, plural category uncertain
ca:%{C%}%{A%} Cl9p1%0 ; ! outside, plural category uncertain
kari:%{K%}ar%{I%} Cl9p1%0 ; ! car, plural category uncertain
benjũ:benj%{Ũ%} Cl9p1%0 ; ! pencil
timũ:%{T%}im%{Ũ%} Cl9p1%0 ; ! lime
koma:%{K%}om%{A%} Cl9p1%0 ; ! spirit, singular category uncertain
metha:meth%{A%} Cl9p1%0 ; ! table
tũmĩrĩri:%{T%}ũmĩrĩr%{I%} Cl9p1%0 ; ! message, plural category uncertain
thimũ:thim%{Ũ%} Cl9p1%0 ; ! phone, plural category uncertain
thitembũ:thitemb%{Ũ%} Cl9p1%0 ; ! stamp, plural category uncertain
bahaca:bahac%{A%} Cl9p1%0 ; ! envelope, plural category uncertain
yama:yam%{A%} Cl9p1%0 ; ! meat, plural category uncertain
gũkũ:gũk%{Ũ%} Cl9p1%0 ; ! chicken, plural category uncertain
thaa:tha%{A%} Cl9p1%0 ; ! hour, plural category uncertain
bakũri:bakũr%{I%} Cl9p1%0 ; 
dahi:dah%{I%} Cl9p1%0 ;
thia:thi%{A%} Cl9p1%0 ;

LEXICON NounRootCl9p1%0Irr

batĩ:bat%{Ĩ%} Cl9p1%0 ;

LEXICON Cl9p1%0

%<n%>%<cl9p1%0%>: NounPluralAgreement ;

LEXICON NounRootCl11p1%0

rũigi:%{R%}ũig%{I%} Cl11p1%0 ;
gĩrĩ:gĩr%{Ĩ%} Cl11p1%0 ; ! fence, plural category uncertain
imbo:%{I%}mb%{O%} Cl11p1%0 ; ! song, plural category uncertain
ũĩ:%{Ũ%}%{Ĩ%} Cl11p1%0 ; ! river, plural category uncertain
ĩgĩ:%{Ĩ%}g%{Ĩ%} Cl11p1%0 ; ! eagle, plural category uncertain

LEXICON Cl11p1%0

%<n%>%<cl11p1%0%>: NounPluralAgreement ;

LEXICON NounRootCl11p6

guoya:guoy%{A%} Cl11p6 ;

LEXICON Cl11p6

%<n%>%<cl11p6%>: NounPluralAgreement ;

LEXICON NounRootCl12p13

nua:nu%{A%} Cl12p13 ;
ana:%{A%}n%{A%} Cl12p13 ; ! child
ihũri:%{I%}hũr%{I%} Cl12p13 ; ! gourd
hũa:hũ%{A%} Cl12p13 ; ! coffee
hĩĩ:hĩ%{Ĩ%} Cl12p13 ; ! boy
hiũ:hi%{Ũ%} Cl12p13 ; ! knife
gui:gu%{I%} Cl12p13 ; ! dog

LEXICON Cl12p13

%<n%>%<cl12p13%>: NounPluralAgreement ;

LEXICON NounRootCl14

wendi:wend%{I%} Cl14 ;
cũrũ:%{C%}ũr%{Ũ%} Cl14 ; ! porridge
goro:gor%{o%} Cl14 ; ! expense

LEXICON Cl14

%<n%>%<cl14%>: NounPluralAgreement ;

LEXICON NounRootCl15

tũ:%{T%}%{Ũ%} Cl15 ;
rug:%{R%}ug Cl15 ; ! cook/cooking
thek:thek Cl15 ; ! laugh
oko:%{O%}k%{O%} Cl15 ;

LEXICON Cl15

%<n%>%<cl15%>: NounPluralAgreement ;

LEXICON NounPrefix

NounPrefixCl1 ;
NounPrefixCl3 ;
NounPrefixCl5 ;
NounPrefixCl7 ;
NounPrefixCl9p1%0 ;
NounPrefixCl11p1%0 ;
NounPrefixCl11p6 ;
NounPrefixCl12p13 ;
NounPrefixCl14 ;
NounPrefixCl15 ;

LEXICON NounPrefixCl1

%[%-sg%]:m%{Ũ%}%> NounRootCl1 ;
%[%-pl%]:%{A%}%> NounRootCl1 ;
%[%-sg%]: NounRootCl1Irr ;
%[%-pl%]: NounRootCl1Irr ;

LEXICON NounPrefixCl3

%[%-sg%]:m%{Ũ%}%> NounRootCl3 ;
%[%-pl%]:m%{Ĩ%}%> NounRootCl3 ;

LEXICON NounPrefixCl5

%[%-sg%]:%{RRI%}%{IRI%}%> NounRootCl5 ;
%[%-pl%]:m%{A%}%> NounRootCl5 ;

LEXICON NounPrefixCl7

%[%-sg%]:%{K%}%{Ĩ%}%> NounRootCl7 ;
%[%-pl%]:%{CC%}%{I%}%> NounRootCl7 ;

LEXICON NounPrefixCl9p1%0

%[%-sg%]:%{N%}%{N2%}%{N3%}%> NounRootCl9p1%0 ;
%[%-pl%]:%{N%}%{N2%}%{N3%}%> NounRootCl9p1%0 ;
%[%-sg%]: NounRootCl9p1%0Irr ;
%[%-pl%]: NounRootCl9p1%0Irr ;

LEXICON NounPrefixCl11p1%0

%[%-sg%]:%{R%}%{Ũ%}%> NounRootCl11p1%0 ;
%[%-pl%]:%{N%}%{N2%}%{N3%}%> NounRootCl11p1%0 ;

LEXICON NounPrefixCl11p6

%[%-sg%]:%{R%}%{Ũ%}%> NounRootCl11p6 ;
%[%-pl%]:m%{A%}%> NounRootCl11p6 ;

LEXICON NounPrefixCl12p13

%[%-sg%]:%{K%}%{A%}%> NounRootCl12p13 ;
%[%-pl%]:%{T%}%{Ũ%}%> NounRootCl12p13 ;

LEXICON NounPrefixCl14

%[%-sg%]: NounRootCl14 ;
%[%-pl%]:m%{A%}%> NounRootCl14 ;

LEXICON NounPrefixCl15

%[%-sg%]:%{K%}%{Ũ%}%> NounRootCl15 ;
%[%-pl%]:m%{A%}%> NounRootCl15 ;


LEXICON VerbPrefix

VerbFoc ;
%[%-imp%]: VerbStem ; 

LEXICON VerbFoc

%[%-foc%]:n%{Ĩ%}%> VerbNeg1 ;
VerbNeg1 ;

LEXICON VerbNeg1

%[%-neg%]:nd%> VerbSubjectConcord ;
VerbSubjectConcord ;

LEXICON VerbSubjectConcord

%[%-p1%]%[%-sg%]:%{N%}%{N2%}%{N3%}%> VerbNeg2 ;
%[%-p1%]%[%-pl%]:%{T%}%{Ũ%}%> VerbNeg2 ;
%[%-p2%]%[%-sg%]:%{Ũ%}%> VerbNeg2 ;
%[%-p2%]%[%-pl%]:m%{Ũ%}%> VerbNeg2 ;
%[%-cl1%]%[%-sg%]:%{A%}%> VerbNeg2 ;
%[%-cl1%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl3%]%[%-sg%]:%{Ũ%}%> VerbNeg2 ;
%[%-cl3%]%[%-pl%]:%{Ĩ%}%> VerbNeg2 ;
%[%-cl5%]%[%-sg%]:r%{Ĩ%}%> VerbNeg2 ;
%[%-cl5%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl7%]%[%-sg%]:%{K%}%{Ĩ%}%> VerbNeg2 ;
%[%-cl7%]%[%-pl%]:%{C%}%{I%}%> VerbNeg2 ;
%[%-cl9p6%]%[%-sg%]:%{Ĩ%}%> VerbNeg2 ;
%[%-cl9p6%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl9p1%0%]%[%-sg%]:%{Ĩ%}%> VerbNeg2 ;
%[%-cl9p1%0%]%[%-pl%]:%{C%}%{I%}%> VerbNeg2 ;
%[%-cl11p6%]%[%-sg%]:%{R%}%{Ũ%}%> VerbNeg2 ;
%[%-cl11p6%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl11p1%0%]%[%-sg%]:%{R%}%{Ũ%}%> VerbNeg2 ;
%[%-cl11p1%0%]%[%-pl%]:%{C%}%{I%}%> VerbNeg2 ;
%[%-cl12p6%]%[%-sg%]:%{K%}%{A%}%> VerbNeg2 ;
%[%-cl12p6%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl12p13%]%[%-sg%]:%{K%}%{A%}%> VerbNeg2 ;
%[%-cl12p13%]%[%-pl%]:%{T%}%{Ũ%}%> VerbNeg2 ;
%[%-cl14%]%[%-sg%]:%{Ũ%}%> VerbNeg2 ;
%[%-cl14%]%[%-pl%]:m%{A%}%> VerbNeg2 ;
%[%-cl15%]%[%-sg%]:%{K%}%{Ũ%}%> VerbNeg2 ;
%[%-cl15%]%[%-pl%]:m%{A%}%> VerbNeg2 ;

LEXICON VerbNeg2

%[%-neg%]:%{T%}%{I%}%> VerbTense ;
VerbTense ;

LEXICON VerbTense

%[%-rempast%]%[%-perf%]%[%-compl%]%[%-hab%]:%{A%}%> VerbObjectAgreement ;
%[%-nearpast%]%[%-perf%]%[%-compl%]%[%-hab%]:r%{A%}%> VerbObjectAgreement ;
%[%-currpast%]%[%-hab%]%[%-perf%]%[%-noasp%]:%{K%}%{Ũ%}%> VerbObjectAgreement ;
%[%-currpast%]%[%-compl%]%[%-noasp%]%[%-hab%]%[%-noasp%]:%{A%}%> VerbObjectAgreement ;
%[%-pres%]%[%-noasp%]:%{K%}%{A%}%> VerbObjectAgreement ; ! existence is dubious, see ndũkagũe, whose meaning is not really what we are going for with present... even though that's how it glosses. appears only in imperatives, conditionals
%[%-pres%]%[%-noasp%]%[%-proc%]:%{RAR%}%{A%}%> VerbObjectAgreement ;
%[%-pres%]%[%-hab%]: VerbObjectAgreement ; ! highly dubious... but exists sometimes like in nĩainaga
%[%-currfut%]%[%-noasp%]%:%{K%}%{Ũ%}%> VerbObjectAgreement ; ! maybe habitual is possible ???
! %[%-currfut%]%[%-fvE%]%[%-hab%]%[%-noasp%]: VerbObjectAgreement ;
%[%-remfut%]%[%-noasp%]:%{K%}%{A%}%> VerbObjectAgreement ;
! %[%-remfut%]%[%-noasp%]%[%-remfutagr%]: VerbObjectAgreement ; ! Difference between ka and null form unclear; may be predictable
%[%-seq%]%[%-noasp%]:%{K%}%{tĨ%}%> VerbObjectAgreement ;

LEXICON VerbObjectAgreement

VerbReflexive ; 

LEXICON VerbReflexive

%[%-refl%]:%{Ĩ%}%> VerbStem ; 
VerbStem ;

LEXICON VerbStem

kiny:%{K%}iny VerbAfterStem ; ! arrive
kũr:%{K%}ũr VerbAfterStem ; ! grow
ũk:%{Ũ%}k VerbAfterStem ; ! come
in:%{I%}n VerbAfterStem ; ! sing
thi:th%{I%} VerbAfterStem ; ! go
!thi%[%-fvĨ%]:th%{I%} VerbAfterStem; ! go
thom:thom VerbAfterStem ; ! read
thamb:thamb VerbAfterStem ;
rĩm:%{R%}ĩm VerbAfterStem ; ! cultivate
haic:haic VerbAfterStem ;
on:on VerbAfterStem ;
humb:humb VerbAfterStem ;
ror:%{R%}or VerbAfterStem ;
gũ:g%{Ũ%} VerbAfterStem ; ! fall
ũr:%{Ũ%}r VerbAfterStem ; ! run away
cok:%{C%}ok VerbAfterStem ;
ik:%{I%}k VerbAfterStem ; ! throw
nyit:nyit VerbAfterStem ; ! catch
tin:tin VerbAfterStem ; ! cut
hanyũk:hanyũk VerbAfterStem ; ! run
ikar:%{I%}kar VerbAfterStem ; ! stay
rĩ:%{R%}%{Ĩ%} VerbAfterStem ; ! eat
ririkan:%{R%}irikan VerbAfterStem ; ! remember
ĩtĩk:%{Ĩ%}tĩk VerbAfterStem ; ! believe
iy:%{I%}y VerbAfterStem ; ! steal
igu:%{I%}g%{U%} VerbAfterStem ; ! hear 
ug:%{U%}g VerbAfterStem ; ! say
ĩr:%{Ĩ%}r VerbAfterStem ; ! tell
dũm:dũm VerbAfterStem ; ! send
tuĩk:%{T%}uĩk VerbAfterStem ; ! become
end:%{E%}nd VerbAfterStem ; ! want
thambĩr:thambĩr VerbAfterStem ; ! swim
ringĩr:%{R%}ingĩr VerbAfterStem ; ! persuade
hing:hing VerbAfterStem ; ! close
amb:%{A%}mb VerbAfterStem ; ! start
ĩrĩgĩr:%{Ĩ%}rĩgĩr VerbAfterStem ; ! intend
ĩtĩkĩr:%{Ĩ%}tĩkĩr VerbAfterStem ; ! allow
rut:%{R%}ut VerbAfterStem ; ! learn 
ribot:%{R%}ibot VerbAfterStem ; ! report
anĩrĩr:%{A%}nĩrĩr VerbAfterStem ; ! announce
on:%{O%}n VerbAfterStem ; ! see
ĩcir:%{Ĩ%}cir VerbAfterStem ; ! think
ĩtu:%{Ĩ%}t%{U%} VerbAfterStem ; ! pretend
rot:%{R%}ot VerbAfterStem ; ! dream
ĩrir:%{ĨE%}rir VerbAfterStem ; ! regret
ken:{%K%}en VerbAfterStem ; ! be happy
rakar:%{R%}akar VerbAfterStem ; ! be angry
ĩhok:%{Ĩ%}hok VerbAfterStem ; ! hope
no:n%{O%} VerbAfterStem ; ! can/must (?)
hot:hot VerbAfterStem ; ! might
tũm:%{T%}ũm VerbAfterStem ; ! 
ũm:%{Ũ%}m VerbAfterStem ; ! dry
hith:hith VerbAfterStem ; ! hide
humb:humb VerbAfterStem ; ! cover


LEXICON VerbAfterStem

%<v%>: VerbTenseAgreement ;
%<v%>%[%+imp%]%<imp%>:%>%{Ĩ%} # ;

LEXICON VerbRecip

%<recip%>:%>%{A%}n VerbIntens ;
VerbIntens ;

LEXICON VerbIntens

%<intens%>:%>%{Ĩ%}rĩr VerbRevers ;
%<intens%>:%>%{Ũ%}rũr VerbRevers ;
VerbRevers ;

LEXICON VerbRevers

%<revers%>:%>%{Ũ%}r VerbCaus ;
VerbCaus ;

LEXICON VerbCaus

%<caus%>:%>%{I%}th VerbApp ;
VerbApp ;

LEXICON VerbApp

%<app%>:%>%{Ĩ%}r VerbAspect ;
VerbAspect ;

LEXICON VerbAspect

%[%+proc%]%[%-fvA%]%<proc%>:%>%{Ĩ%}r VerbTrans ;
%[%+hab%]%[%-fvA%]%<hab%>:%>%{A%}g VerbTrans ;
%[%+perf%]%[%-fvE%]%<perf%>:%>%{Ĩ%}t VerbTrans ;
%[%+compl%]%[%-fvE%]%<compl%>:%>%{I%}r VerbTrans ;
! %[%+hab%]%<hab%>:%>%{A%}g VerbTrans ;
! %[%+noasp%]: VerbTrans ;
! %<prog%>: VerbTrans ;
%[%+noasp%]%[%-fvA%]: VerbTrans ;

LEXICON VerbTrans

%<trans%>:%>%{I%} VerbFinalVowel ;
VerbFinalVowel ;

LEXICON VerbFinalVowel

%[%+fvE%]:%>%{EE%} VerbReflexiveAgreement ;
%[%+fvA%]:%>%{A%} VerbReflexiveAgreement ;
! %[%+fvĨ%]:%>%{Ĩ%} VerbReflexiveAgreement ;
! %[%+nofv%]: VerbReflexiveAgreement ;
! VerbReflexiveAgreement ;

LEXICON VerbReflexiveAgreement 

%[%+refl%]%<refl%>: VerbNegAgreement ;
VerbNegAgreement ;

LEXICON VerbNegAgreement

%[%+neg%]%<neg%>: VerbSubjectConcordAgreement ;
VerbSubjectConcordAgreement ;

LEXICON VerbTenseAgreement

%[%+rempast%]%<rempast%>: VerbRecip ;
%[%+nearpast%]%<nearpast%>: VerbRecip ;
%[%+currpast%]%<currpast%>: VerbRecip ;
%[%+pres%]%<pres%>: VerbRecip ;
%[%+currfut%]%<currfut%>: VerbRecip ;
! %[%+nearfut%]%<nearfut%>: VerbRecip ;
%[%+remfut%]%<remfut%>: VerbRecip ;
%[%+seq%]%<seq%>: VerbRecip ;

LEXICON VerbSubjectNumberAgreement

%[%+sg%]%<sg%>: VerbFocAgreement ;
%[%+pl%]%<pl%>: VerbFocAgreement ;

LEXICON VerbSubjectConcordAgreement

%[%+p1%]%<p1%>: VerbSubjectNumberAgreement ;
%[%+p2%]%<p2%>: VerbSubjectNumberAgreement ;
%[%+cl1%]%<p3%>%<cl1%>: VerbSubjectNumberAgreement ; ! Noun class 1
%[%+cl3%]%<p3%>%<cl3%>: VerbSubjectNumberAgreement ; ! Noun class 3
%[%+cl5%]%<p3%>%<cl5%>: VerbSubjectNumberAgreement ; ! Noun class 6
%[%+cl7%]%<p3%>%<cl7%>: VerbSubjectNumberAgreement ; ! Noun class 7
%[%+cl9p6%]%<p3%>%<cl9p6%>: VerbSubjectNumberAgreement ;   ! Noun class 9p6
%[%+cl9p1%0%]%<p3%>%<cl9p1%0%>: VerbSubjectNumberAgreement ; ! Noun class 9p1%0
%[%+cl11p6%]%<p3%>%<cl11p6%>: VerbSubjectNumberAgreement ; ! Noun class 11p6
%[%+cl11p1%0%]%<p3%>%<cl11p1%0%>: VerbSubjectNumberAgreement ; ! Noun class 11p1%0
%[%+cl12p6%]%<p3%>%<cl12p6%>: VerbSubjectNumberAgreement  ; ! Noun class 12p6
%[%+cl12p13%]%<p3%>%<cl12p13%>: VerbSubjectNumberAgreement ; ! Noun class 12p13
%[%+cl14%]%<p3%>%<cl14%>: VerbSubjectNumberAgreement ;   ! Noun class 14
%[%+cl15%]%<p3%>%<cl14%>: VerbSubjectNumberAgreement ;   ! Noun class 15

LEXICON VerbFocAgreement

%[%+foc%]%<foc%>: # ;
# ;

LEXICON AdjPrefix

%[%-cl1%]%[%-sg%]:m%{Ũ%} AdjRoot ;
%[%-cl1%]%[%-pl%]:a AdjRoot ;
%[%-cl3%]%[%-sg%]:m%{Ũ%} AdjRoot ;
%[%-cl3%]%[%-pl%]:mĩ AdjRoot ;
%[%-cl5%]%[%-sg%]:%{RRI%}%{IRI%} AdjRoot ;
%[%-cl5%]%[%-pl%]:ma AdjRoot ;
%[%-cl7%]%[%-sg%]:%{K%}ĩ AdjRoot ;
%[%-cl7%]%[%-pl%]:%{N%}%{N2%}%{N3%} AdjRoot ;
%[%-cl9p6%]%[%-sg%]:%{N%}%{N2%}%{N3%} AdjRoot ;
%[%-cl9p6%]%[%-pl%]:ma AdjRoot ;
%[%-cl9p1%0%]%[%-sg%]:%{N%}%{N2%}%{N3%} AdjRoot ;
%[%-cl9p1%0%]%[%-pl%]:%{N%}%{N2%}%{N3%} AdjRoot ;
%[%-cl11p6%]%[%-sg%]:rũ AdjRoot ;
%[%-cl11p6%]%[%-pl%]:ma AdjRoot ;
%[%-cl11p1%0%]%[%-sg%]:rũ AdjRoot ;
%[%-cl11p1%0%]%[%-pl%]:%{N%}%{N2%}%{N3%} AdjRoot ;
%[%-cl12p6%]%[%-sg%]:ka AdjRoot ;
%[%-cl12p6%]%[%-pl%]:ma AdjRoot ;
%[%-cl12p13%]%[%-sg%]:%{K%}a AdjRoot ;
%[%-cl12p13%]%[%-pl%]:t%{Ũ%} AdjRoot ;
%[%-cl14%]%[%-sg%]:m%{Ũ%} AdjRoot ;
%[%-cl14%]%[%-pl%]:ma AdjRoot ;
%[%-cl15%]%[%-sg%]:%{K%}%{Ũ%} AdjRoot ;
%[%-cl15%]%[%-pl%]:ma AdjRoot ;

LEXICON AdjRoot

nene:nene AdjAgree ; ! big
kũrũ:kũrũ AdjAgree ; ! old
ega:ega AdjAgree ; ! good
tune:tune AdjAgree ; ! red
rũarũ:rũarũ AdjAgree ; ! sick
nini:nini AdjAgree ; ! small/young
erũ:erũ AdjAgree ; ! neq
ũru:ũru AdjAgree ; ! bad
erũ:erũ AdjAgree ; ! white
athĩki:athĩki AdjAgree ; ! obedient
kuhĩ:kuhĩ AdjAgree ; ! short
kenge:kenge AdjAgree ; ! baby
thaka:thaka AdjAgree ; ! beautiful
irũ:irũ AdjAgree ; ! black
raihu:raihu AdjAgree ; ! tall

LEXICON AdjAgree

%<adj%>: AdjClassAgree ;

LEXICON AdjClassAgree

%[%+cl1%]%<cl1%>: AdjNumberAgree ; ! Noun class 1
%[%+cl3%]%<cl3%>: AdjNumberAgree ; ! Noun class 3
%[%+cl5%]%<cl5%>: AdjNumberAgree ; ! Noun class 6
%[%+cl7%]%<cl7%>: AdjNumberAgree ; ! Noun class 7
%[%+cl9p6%]%<cl9p6%>: AdjNumberAgree ;   ! Noun class 9p6
%[%+cl9p1%0%]%<cl9p1%0%>: AdjNumberAgree ; ! Noun class 9p1%0
%[%+cl11p6%]%<cl11p6%>: AdjNumberAgree ; ! Noun class 11p6
%[%+cl11p1%0%]%<cl11p1%0%>: AdjNumberAgree ; ! Noun class 11p1%0
%[%+cl12p6%]%<cl12p6%>: AdjNumberAgree ; ! Noun class 12p6
%[%+cl12p13%]%<cl12p13%>: AdjNumberAgree ; ! Noun class 12p13
%[%+cl14%]%<cl14%>: AdjNumberAgree ;   ! Noun class 14
%[%+cl15%]%<cl15%>: AdjNumberAgree ;   ! Noun class 15

LEXICON AdjNumberAgree

%[%+sg%]%<sg%>: # ;
%[%+pl%]%<pl%>: # ;

LEXICON Assoc

a%<assoc%>%<cl1%>%<sg%>:wa # ;
a%<assoc%>%<cl1%>%<pl%>:a # ;
a%<assoc%>%<cl3%>%<sg%>:wa # ;
a%<assoc%>%<cl3%>%<pl%>:ya # ;
a%<assoc%>%<cl5%>%<sg%>:rĩa # ;
a%<assoc%>%<cl5%>%<pl%>:ma # ;
a%<assoc%>%<cl9p6%>%<pl%>:ma # ;
a%<assoc%>%<cl11p6%>%<pl%>:ma # ;
a%<assoc%>%<cl7%>%<sg%>:kĩa # ;
a%<assoc%>%<cl7%>%<pl%>:cia # ;
a%<assoc%>%<cl9p6%>%<sg%>:ya # ;
a%<assoc%>%<cl9p1%0%>%<sg%>:ya # ;
a%<assoc%>%<cl9p1%0%>%<pl%>:cia # ;
a%<assoc%>%<cl11p1%0%>%<pl%>:cia # ;
a%<assoc%>%<cl11p6%>%<sg%>:rũa # ;
a%<assoc%>%<cl11p1%0%>%<sg%>:rũa # ;
a%<assoc%>%<cl12p13%>%<sg%>:ka # ;
a%<assoc%>%<cl12p13%>%<pl%>:tũa # ; 
a%<assoc%>%<cl14%>%<sg%>:wa # ;
a%<assoc%>%<cl14%>%<pl%>:ma # ;
a%<assoc%>%<cl15%>%<sg%>:kũa # ;
a%<assoc%>%<cl15%>%<pl%>:ma # ;

LEXICON PersPro

%<pro%>%<p1%>%<sg%>:niũ # ;
%<pro%>%<p1%>%<pl%>:ithuĩ # ; 
%<pro%>%<p2%>%<sg%>:weũ # ;
%<pro%>%<p2%>%<pl%>:inyuũ # ;
o%<pro%>%<p3%>%<cl1%>%<sg%>:we # ; 
o%<pro%>%<p3%>%<cl1%>%<pl%>:mo # ;
o%<pro%>%<p3%>%<cl3%>%<sg%>:guo # ;
o%<pro%>%<p3%>%<cl3%>%<pl%>:yo # ;
o%<pro%>%<p3%>%<cl5%>%<sg%>:rĩo # ;
o%<pro%>%<p3%>%<cl5%>%<pl%>:mo # ;
o%<pro%>%<p3%>%<cl7%>%<sg%>:kĩo # ;
o%<pro%>%<p3%>%<cl7%>%<pl%>:cio # ;
o%<pro%>%<p3%>%<cl9p1%0%>%<sg%>:yo # ;
o%<pro%>%<p3%>%<cl9p1%0%>%<pl%>:cio # ;
o%<pro%>%<p3%>%<cl11p6%>%<sg%>:ruo # ;
o%<pro%>%<p3%>%<cl11p6%>%<pl%>:mo # ;
o%<pro%>%<p3%>%<cl11p1%0%>%<sg%>:ruo # ;
o%<pro%>%<p3%>%<cl11p1%0%>%<pl%>:cio # ;
o%<pro%>%<p3%>%<cl12p13%>%<sg%>:ko # ;
o%<pro%>%<p3%>%<cl12p13%>%<pl%>:tuo # ;
o%<pro%>%<p3%>%<cl14%>%<sg%>:guo # ;
o%<pro%>%<p3%>%<cl14%>%<pl%>:mo # ;
o%<pro%>%<p3%>%<cl15%>%<sg%>:kuo # ;
o%<pro%>%<p3%>%<cl15%>%<pl%>:mo # ;

LEXICON LittleWord

na%<prep%>:na # ; ! with
o%<quant%>:o # ; ! all

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst



